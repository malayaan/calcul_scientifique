// Parameters
int inside = 2; int outside = 1; int n = 30;

// Mesh
border a(t=0, 0.4) { x=1; y=t; label=outside;};
border b(t=0.75, 1) { x=t; y=0; label=outside;};
border c(t=0.4, 0) { x=0.75; y=t; label=outside;};
border d(t=1, 0.75) { x=t; y=0.4; label=inside;};


//superieur
border d1(t=1, 0.75) { x=t; y=1; label=outside; };
border d2(t=1, 0.4) { x=0.75; y=t; label=outside; };
border d3(t=0.75, 1) { x=t; y=0.4; label=inside; };
border d4(t=0.4, 0.6) { x=1; y=t; label=outside; };
border d5(t=-pi/2, pi/2) { x=1+0.2*cos(t); y=0.8+0.2*sin(t); label=outside; };

//interieur
border i1(t=1, 0.85) { x=t; y=0.90; label=outside; };
border i2(t=0.85, 1) { x=t; y=0.70; label=outside; };
border i3(t=0.90, 0.70) { x=0.85; y=t; label=outside; };
border i4(t=-pi/2, pi/2) { x=1+0.1*cos(t); y=0.80+0.1*sin(t); label=outside; };


mesh th = buildmesh(a(n) + b(n) + c(n) + d(n));
mesh TH = buildmesh(d1(n)+d2(n)+d3(n)+d4(n)+d5(n)+i1(-n)+i2(-n)+i3(-n)+i4(-n));
plot(th, TH, wait=true);

// Fespace
fespace vh(th, P1);
vh u=0, v;
vh lambda=0;

fespace VH(TH, P1);
VH U, V;

// Problem
int i = 0;
problem PB (U, V, init=i, solver=Cholesky)
    = int2d(TH)(
          dx(U)*dx(V)
        + dy(U)*dy(V)
    )
    + int2d(TH)(
        - V
    )
    + int1d(TH, inside)(
          lambda*V
    )
    + on(outside, U= 0 )
    ;

problem pb (u, v, init=i, solver=Cholesky)
    = int2d(th)(
          dx(u)*dx(v)
        + dy(u)*dy(v)
    )
    + int2d(th)(
        - v
    )
    + int1d(th, inside)(
        - lambda*v
    )
    + on(outside, u=0)
    ;

for (i = 0; i < 20; i++){
    // Solve
    PB;
    pb;
    lambda = lambda - (u-U)/2;

    // Plot
    plot(U,u,wait=0);
}

// Plot
plot(U, u, wait=1);